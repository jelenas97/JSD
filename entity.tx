EntityModel:
    entities += Entity        // Each model has one or more entities.
    shared = Shared?
    pages = Pages?
;

Entity:
    'entity' name=ID ':'
        propertiesPart *= PropertyPart    // Each entity has zero or one!!! property part
        operationsPart *= OperationPart   // Each entity has zero or one operations part
;

PropertyPart:
    'properties:'
        properties += Property
;

Property:
    annotations *= Annotation
    name = ID '>' type = Type
;

Type:
    type = [RefferenceType] | '[' entity=[RefferenceType] ']'
;

Annotation:
    '{' annotation = [AnnotationName] '}'
;

AnnotationName:
    'annotation' name = ID
;

// Type can be SimpleType or Entity
RefferenceType:
    SimpleType | Entity
;

SimpleType:
    'type' name = ID
;

// Operation part
OperationPart:
    'operations:'
        operations += Operation
;

Operation:
    name = ID '(' parameters *= Parameters ')' type = Type
;

Parameters:
    name = ID ':' type=[RefferenceType] ',' parameters += Parameters | name=ID ':' type=[RefferenceType]
;

//Shared part

Shared:
    'shared[' entities += [Entity][','] ']:'
        propertiesPart *= PropertyPart
        operationsPart *= SharedOperationsPart
;

SharedOperationsPart:
    'operations:'
        operations += SimpleOperation
;

SimpleOperation:
    UPDATE ?= 'update' | SAVE ?= 'save' | DELETE ?= 'delete' | GET_BY_ID ?= 'getById' | GET_ALL ?= 'getAll'
;

//Pages part

Pages:
    'pages[' entities *= [Entity][','] ']:'
        pagesTypePart *= PagesTypePart
;

PagesTypePart:
    VIEW ?= 'view' | ADD ?= 'add' | EDIT ?= 'edit'
;


// Special rule for comments. Comments start with //
Comment:
    /\/\/.*$/
;
